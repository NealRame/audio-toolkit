# Try to find LibVORBISENC
# Once done this will define
#  LIBVORBISENC_FOUND - System has LibVORBISENC
#  LIBVORBISENC_INCLUDE_DIRS - The LibVORBISENC include directories
#  LIBVORBISENC_LIBRARIES - The libraries needed to use LibVORBISENC
#  LIBVORBISENC_DEFINITIONS - Compiler switches required for using LibVORBISENC

include(FindPackageHandleStandardArgs)

find_package(PkgConfig)

pkg_check_modules(PC_LIBVORBISENC QUIET vorbisenc)
set(LIBVORBISENC_DEFINITIONS ${PC_LIBVORBISENC_CFLAGS_OTHER})

find_path(
	LIBVORBISENC_INCLUDE_DIR 
	NAMES vorbis/vorbisenc.h
	HINTS ${PC_LIBVORBISENC_INCLUDEDIR} ${PC_LIBVORBISENC_INCLUDE_DIRS}
)

find_library(
	LIBVORBISENC_LIBRARY NAMES vorbisenc libvorbisenc
	HINTS ${PC_LIBVORBISENC_LIBDIR} ${PC_LIBVORBISENC_LIBRARY_DIRS}
)

set(LIBVORBISENC_INCLUDE_DIRS ${LIBVORBISENC_INCLUDE_DIR})
set(LIBVORBISENC_LIBRARIES ${LIBVORBISENC_LIBRARY})
set(LIBVORBISENC_LIBRARY_DIRS ${PC_LIBVORBISENC_LIBRARY_DIRS})

# handle the QUIETLY and REQUIRED arguments and set LIBVORBISENC_FOUND to TRUE
# if all listed variables are TRUE
find_package_handle_standard_args(
	LibVORBISENC DEFAULT_MSG
	LIBVORBISENC_LIBRARY LIBVORBISENC_INCLUDE_DIR
)

mark_as_advanced(LIBVORBISENC_INCLUDE_DIR LIBVORBISENC_LIBRARY)
